#!/bin/csh
# This script initialises a work dir after CVS checkout
# $Id: setup_cvstree,v 1.8 2007-12-10 04:04:19 cvsnanne Exp $

if ( $#argv < 1 ) then
    set od_wdir=`pwd`
    echo "Assuming that pwd is OD work directory: $od_wdir"
    echo -n "If not ok, ^C out"
    set tmp=$<
else
    set od_wdir="$1"
endif

if ( ! -d "$od_wdir" ) then
    echo "Cannot find workdir: $od_wdir"
    exit 1
endif

cd "$od_wdir"
if ( ! $?DGB_CVS_DIR ) then
    setenv DGB_CVS_DIR $od_wdir/dvs
    if ( $?WORK ) then
        if ( -e $WORK/dvs ) setenv DGB_CVS_DIR $WORK/dvs
    endif
endif
$DGB_CVS_DIR/dvs update

ln -s Pmake/RootMakefile Makefile

cd src
ln -s .mods.od .mods

source $od_wdir/bin/mksethdir

# make G and O dirs in $WORK/lib/$binsubdir
if ( ! -e "$od_wdir/lib" ) mkdir -p "$od_wdir/lib"

cd "$od_wdir/lib"
if ( ! -e $binsubdir ) mkdir $binsubdir

cd $binsubdir
if ( ! -e G ) mkdir G
if ( ! -e O ) mkdir O
cd G
ln -s ../*.a . >& /dev/null
cd ../O
ln -s ../*.a . >& /dev/null

# make G and O dirs in $WORK/bin/$binsubdir
if ( ! -e "$od_wdir/bin" ) mkdir -p "$od_wdir/bin"

cd "$od_wdir/bin"
if ( ! -e $binsubdir ) mkdir $binsubdir

cd $binsubdir
if ( ! -e so ) mkdir so
if ( ! -e G ) mkdir G
if ( ! -e O ) mkdir O
cd G
if ( ! -e so ) ln -s ../so .
cd ../O
if ( ! -e so ) ln -s ../so .

ln -s ../lmhostid* .

cd "$od_wdir/plugins"
if ( ! -e Makefile ) ln -s Makefile.od Makefile
cd "$od_wdir/spec"
if ( ! -e Makefile ) ln -s Makefile.od Makefile


# create documentation dirs

if ( -e $od_wdir/data ) then
    if ( ! -e ${od_wdir}/data/dTectDoc ) mkdir ${od_wdir}/data/dTectDoc
    if ( ! -e ${od_wdir}/data/dTectDoc/Programmer ) then
	mkdir ${od_wdir}/data/dTectDoc/Programmer
    endif
    if ( ! -e ${od_wdir}/data/dTectDoc/Programmer/Generated ) then
	mkdir ${od_wdir}/data/dTectDoc/Programmer/Generated
    endif
endif

# copy relevant libraries to $WORK/bin/$binsubdir/so
cd "$od_wdir/bin/$binsubdir/so"

if ( $binsubdir == lux32 ) then
    cp /usr/lib/libstdc++.so.6 .
    cp /lib/libgcc_s.so.1 .
else if ( $binsubdir == lux64 ) then
    cp /lib64/libgcc_s.so.1 .
    cp /usr/lib64/libstdc++.so.6 .
else if ( $binsubdir == sol32 ) then
    cp /usr/local/lib/libgcc_s.so.1 .
    cp /usr/local/lib/libstdc++.so.6 .
endif

set dosimvollib=0
if ( ! -e /users/appman/bin/set_basedir.csh ) then
    echo "make sure coin, qt and fftw .so libs are"
    echo "copied or linked to $od_wdir/bin/$binsubdir/so"
    echo "See the end $0 for details on what needs to be linked"
else if ( $binsubdir == mac ) then
    source /users/appman/bin/set_basedir.csh
    ln -s $basedir/qt/rel/lib/libQt3Support.4.dylib .
    ln -s $basedir/qt/rel/lib/libQtCore.4.dylib .
    ln -s $basedir/qt/rel/lib/libQtGui.4.dylib .
    ln -s $basedir/qt/rel/lib/libQtNetwork.4.dylib .
    ln -s $basedir/qt/rel/lib/libQtOpenGL.4.dylib .
    ln -s $basedir/qt/rel/lib/libQtSql.4.dylib .
    ln -s $basedir/qt/rel/lib/libQtXml.4.dylib .
    ln -s $basedir/coin/lib/libCoin.40.dylib .
    ln -s $basedir/coin/lib/libSoQt.20.dylib .
    ln -s $basedir/coin/lib/libsimage.20.dylib .
    ln -s libsimage.20.dylib libsimage.dylib
    ln -s $basedir/fftw/lib/libfftw3f.3.dylib .
    ln -s libfftw3f.3.dylib libfftw3f.dylib
    set dosimvollib=1
else
    source /users/appman/bin/set_basedir.csh
    ln -s $basedir/qt/rel/lib/libQt3Support.so.4 .
    ln -s $basedir/qt/rel/lib/libQtCore.so.4 .
    ln -s $basedir/qt/rel/lib/libQtGui.so.4 .
    ln -s $basedir/qt/rel/lib/libQtNetwork.so.4 .
    ln -s $basedir/qt/rel/lib/libQtOpenGL.so.4 .
    ln -s $basedir/qt/rel/lib/libQtSql.so.4 .
    ln -s $basedir/qt/rel/lib/libQtXml.so.4 .
    ln -s $basedir/coin/lib/libCoin.so.40 .
    ln -s $basedir/coin/lib/libSoQt.so.20 .
    ln -s $basedir/coin/lib/libsimage.so.20 .
    ln -s libsimage.so.20 libsimage.so
    ln -s $basedir/fftw/lib/libfftw3f.so.3 .
    ln -s libfftw3f.so.3 libfftw3f.so
    set dosimvollib=1
endif

if ( $dosimvollib == 1 ) then
    cd "$od_wdir/lib/$binsubdir"
    ln -s $basedir/coin/lib/libSimVoleon.a .
    cd G
    ln -s ../libSimVoleon.a . >& /dev/null
    cd ../O
    ln -s ../libSimVoleon.a . >& /dev/null
endif
