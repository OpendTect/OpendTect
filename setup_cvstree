#!/bin/csh
# This script initialises a work dir after CVS checkout
# $Id: setup_cvstree,v 1.17 2009-12-16 11:24:27 cvsnanne Exp $

if ( $#argv < 1 ) then
    set od_wdir=`pwd`
    echo "Assuming that pwd is OD work directory: $od_wdir"
    echo -n "If not ok, ^C out"
    set tmp=$<
else
    set od_wdir="$1"
endif

if ( ! -d "$od_wdir" ) then
    echo "Cannot find workdir: $od_wdir"
    exit 1
endif

cd "$od_wdir"
cvs -q update -d -P

ln -s Pmake/RootMakefile Makefile

cd src
ln -s .mods.od .mods

setenv DTECT_APPL $od_wdir
source $od_wdir/bin/mksethdir

# make G and O dirs in $WORK/lib/$binsubdir
if ( ! -e "$od_wdir/lib" ) mkdir -p "$od_wdir/lib"

cd "$od_wdir/lib"
if ( ! -e $binsubdir ) mkdir $binsubdir

cd $binsubdir
if ( ! -e G ) mkdir G
if ( ! -e O ) mkdir O
cd G
ln -s ../*.a . >& /dev/null
cd ../O
ln -s ../*.a . >& /dev/null

# make G and O dirs in $WORK/bin/$binsubdir
if ( ! -e "$od_wdir/bin" ) mkdir -p "$od_wdir/bin"

cd "$od_wdir/bin"
if ( ! -e $binsubdir ) mkdir $binsubdir

cd $binsubdir
if ( ! -e so ) mkdir so
if ( ! -e G ) mkdir G
if ( ! -e O ) mkdir O
cd G
if ( ! -e so ) ln -s ../so .
cd ../O
if ( ! -e so ) ln -s ../so .

ln -s ../lmhostid* .

cd "$od_wdir/plugins"
if ( ! -e Makefile ) then
    ln -s Makefile.od Makefile
endif
cd "$od_wdir/spec"
if ( ! -e Makefile ) then
    ln -s Makefile.od Makefile
endif


# copy relevant libraries to $WORK/bin/$binsubdir/so
cd "$od_wdir/bin/$binsubdir/so"

if ( $binsubdir == lux32 ) then
    cp /usr/lib/libstdc++.so.6 .
    cp /lib/libgcc_s.so.1 .
    cp /lib/tls/libpthread.so.0 .
else if ( $binsubdir == lux64 ) then
    cp /lib64/libgcc_s.so.1 .
    cp /usr/lib64/libstdc++.so.6 .
    cp /lib64/tls/libpthread.so.0 .
else if ( $binsubdir == sol32 ) then
    cp /usr/local/lib/libgcc_s.so.1 .
    cp /usr/local/lib/libstdc++.so.6 .
endif

if ( $binsubdir == mac ) then
    cd "$od_wdir/bin/$binsubdir"
    cp /usr/X11/bin/glxinfo .
    cd O
    ln -s ../glxinfo .
endif

set qtdir=
set coindir=
set fftwdir=
set dosimvollib=0
set basedirsetscr="$od_wdir"/buildscripts/set_basedir.csh
if ( ! -e $basedirsetscr ) set basedirsetscr=/users/appman/bin/set_basedir.csh
if ( -e $basedirsetscr ) then
    source $basedirsetscr
    set qtdir=$BASE_DIR/qt/devel
    set coindir=$BASE_DIR/coin/devel
    set fftwdir=$BASE_DIR/fftw/devel
endif
if ( $?OD_QTDIR ) then
    set qtdir=$OD_QTDIR
endif
if ( $?OD_COINDIR ) then
    set coindir=$OD_COINDIR
endif
if ( $?OD_FFTWDIR ) then
    set fftwdir=$OD_FFTWDIR
endif

set missingpkgnms=
if ( $qtdir == "" ) then
    set missingpkgnms="Qt"
endif
if ( $coindir == "" ) then
    set missingpkgnms="$missingpkgnms Coin"
endif
if ( $fftwdir == "" ) then
    set missingpkgnms="$missingpkgnms FFTW"
endif

if ( ${missingpkgnms} != "" ) then
    echo "Make sure $missingpkgnms .so libs are"
    echo "copied or linked to $od_wdir/bin/$binsubdir/so"
    echo "See the end $0 for details on what needs to be linked"
endif


cd "$od_wdir/bin/$binsubdir/so"
if ( $binsubdir == mac ) then
    if ( $qtdir != "" ) then
	ln -s $qtdir/lib/libQtCore.4.dylib .
	ln -s $qtdir/lib/libQtGui.4.dylib .
	ln -s $qtdir/lib/libQtNetwork.4.dylib .
	ln -s $qtdir/lib/libQtOpenGL.4.dylib .
	ln -s libQtOpenGL.4.dylib libQtOpenGL.dylib
	ln -s $qtdir/lib/libQtSql.4.dylib .
	ln -s $qtdir/lib/libQtXml.4.dylib .
	ln -s $qtdir/lib/libQt3Support.4.dylib .
    endif
    if ( $coindir != "" ) then
	ln -s $coindir/lib/libCoin.60.dylib .
	ln -s $coindir/lib/libSoQt.20.dylib .
	ln -s $coindir/lib/libsimage.20.dylib .
	ln -s libsimage.20.dylib libsimage.dylib
	set dosimvollib=1
    endif
    if ( $fftwdir != "" ) then
	ln -s $fftwdir/lib/libfftw3f.3.dylib .
	ln -s libfftw3f.3.dylib libfftw3f.dylib
    endif
else
    if ( $qtdir != "" ) then
	ln -s $qtdir/lib/libQtCore.so.4 .
	ln -s $qtdir/lib/libQtGui.so.4 .
	ln -s $qtdir/lib/libQtNetwork.so.4 .
	ln -s $qtdir/lib/libQtOpenGL.so.4 .
	ln -s $qtdir/lib/libQtSql.so.4 .
	ln -s $qtdir/lib/libQtXml.so.4 .
	ln -s $qtdir/lib/libQt3Support.so.4 .
    endif
    if ( $coindir != "" ) then
	ln -s $coindir/lib/libCoin.so.60 .
	ln -s $coindir/lib/libSoQt.so.20 .
	ln -s $coindir/lib/libsimage.so.20 .
	ln -s libsimage.so.20 libsimage.so
	set dosimvollib=1
    endif
    if ( $fftwdir != "" ) then
	ln -s $fftwdir/lib/libfftw3f.so.3 .
	ln -s libfftw3f.so.3 libfftw3f.so
    endif
endif

if ( $dosimvollib == 1 ) then
    cd "$od_wdir/lib/$binsubdir"
    ln -s $coindir/lib/libSimVoleon.a .
    cd G
    ln -s ../libSimVoleon.a . >& /dev/null
    cd ../O
    ln -s ../libSimVoleon.a . >& /dev/null
endif

cd $od_wdir
foreach fil ( init.bash init.csh )
    sed -e "s%__WORK__%$od_wdir%" \
	-e "s%__DTECTAPPL__DIR__%$od_wdir%" \
	"$od_wdir/dtect/dtectspec/.$fil" > $fil
end
