#!/bin/csh -f

#Changing the dependency path of the libraries on MAC platform.
if ( $#argv < 2 ) then
   echo "Usage : $0 fromdir installdir"
   exit 1
endif

set workdir=$1
set binpath=$2
echo "workdir: $workdir"
echo "binpath: $binpath"
cp -pf $workdir/* $binpath/.
echo "Changing the dependency path in : $binpath"
foreach bnry ( ${binpath}/* )
    if ( -f ${bnry} ) then
	echo ${bnry}
	set filenm=`basename $bnry`
	echo ${filenm}
	set deplist=`otool -L ${bnry} | sed 's/ .*//'`
	foreach dep ( ${deplist} )
	    if ( -e ${dep} ) then
		echo "Dep: ${dep}"
		set libnm=`basename ${dep}`
		set rootdir=`echo ${dep} | cut -c 2-4`
		set insttest="${dep}"
		set depmodified=`echo "${dep}" | sed 's/\//_/g'`
		set test=`echo "${depmodified}" | awk '{n=split($0,a,"_"); print a[n]}'`
		
		set chgdep=false
		if ( ${test} == "libfontconfig.1.dylib" || ${test} == "libfreetype.6.dylib" || ${test} == "libcrypto.1.1.dylib" ) then
		    set chgdep=true
		endif

		if ( $rootdir != "Sys" && ${chgdep} == "true" ) then
		    install_name_tool -change ${dep} @executable_path/../Frameworks/${libnm} ${bnry}
		endif
	    endif
	end
    endif
end
