#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id$
#
# Sets up the python environments (version 1.2.5)
#_______________________________________________________________________________

if ( "$1" == "--help" ) then
    echo "This script sets up the python environments"
    echo "You can run this script at any time"
    echo "If you add new python environments or move the installation directory, you have to run this script"
    exit 0
endif

# this fetches the original dir from shebang
set origcondapos=`cat bin/conda | head -n 1 | awk '{print substr($0, 3)}' | awk '{print index($0,"/miniconda3/")}' | bc`
set origcondalen=`echo $origcondapos+10`
if ( $origcondapos == 0 ) then
        set origcondapos=`cat bin/conda | head -n 1 | awk '{print substr($0, 3)}' | awk '{print index($0,"/Python/")}' | bc`
        set origcondalen=`echo $origcondapos+6`
endif
set origdir=`cat bin/conda | head -n 1 | awk '{print substr($0, 3, '$origcondalen')}'`


# at this point current directory is installation directory.
set findspace = `pwd | grep -e '^\s[^\s]' -e '[^\s]\s$' -e '[^\s]\s[^\s]' | wc | awk '{print $1}'`
if ( $findspace > 0 ) then
    echo "Warning: cannot setup Python environments when there is 1 or more spaces in the path."
    echo "Will exit the setup now..."
    exit 1
endif

set condainstdir=`pwd`

if ( ! -w "${condainstdir}" ) then
    echo "Warning: cannot properly setup Python environments."
    echo "Reason: ${condainstdir} is not writable."
    echo "Will exit the setup now..."
    exit 1
endif

if ( ! -w "${condainstdir}"/envs ) then
    echo "Warning: cannot properly setup Python environments."
    echo "Reason: ${condainstdir}/envs is not writable."
    echo "Will exit the setup now..."
    exit 1
endif

# gets environment dirs and sets which dirs to walk through
set envdirs=`ls $condainstdir/envs/`
set envrepldirs='bin conda-meta etc/dbus-1 etc/fonts include include/libxslt lib lib/cairo lib/cmake/libxml2 lib/cmake/ZeroMQ lib/graphviz lib/gstreamer-1.0 lib/icu lib/icu/58.2 lib/icu/current lib/itcl4.1.1 lib/itcl4.2.0 lib/pkgconfig lib/python3.7 lib/python3.7/config-3.7m-x86_64-linux-gnu lib/python3.7/site-packages lib/python3.7/site-packages/nbconvert/tests/exporter_entrypoint lib/python3.7/site-packages/numpy lib/python3.7/site-packages/numpy/distutils lib/python3.7/site-packages/scipy lib/python3.7/site-packages/zmq/utils lib/tdbc1.0.6 lib/tdbc1.1.1 mkspecs mkspecs/modules share/dbus-1 share/doc/dbus/examples share/jupyter/kernels/python3 ssl/misc'
set instrepldirs='bin condabin conda-meta etc/fish/conf.d etc/profile.d include/libxslt lib lib/cmake/libxml2 lib/icu lib/icu/58.2 lib/icu/current lib/itcl4.1.1 lib/itcl4.2.0 lib/pkgconfig lib/python3.7 lib/python3.7/config-3.7m-x86_64-linux-gnu lib/python3.7/site-packages/numpy lib/python3.7/site-packages/numpy/distutils lib/python3.7/site-packages/xontrib lib/tdbc1.0.6 lib/tdbc1.1.1 shell/condabin ssl/misc'

# replace paths in miniconda3 dir
echo "Setting up miniconda3..."
foreach instrepldir ( $instrepldirs)
	if ( ! -w "${condainstdir}/${instrepldir}" ) then
		chmod og+w ${condainstdir}/${instrepldir}
	endif
        if ( -w "${condainstdir}/${instrepldir}" ) then
                foreach fil (`ls --hide='~*' $condainstdir/$instrepldir`)
                        set fildir=`file $condainstdir/$instrepldir/$fil | grep text | awk '{print $1}' | sed 's/.$//'`
                        if ("$fildir" != "") then
                                sed -i -- s,$origdir,$condainstdir,g $fildir
                        endif
                end
        else
                echo "Warning: ${condainstdir}/${instrepldir} is not writable or does not exist, therefore skipping ${condainstdir}/${instrepldir}."
        endif
end

# replace paths in environment dirs
if ("$envdirs" != "") then
        foreach envdir ( $envdirs )
                echo "Setting up environment $envdir..."
		set origcondapos=`cat $condainstdir/envs/$envdir/bin/pip | head -n 1 | awk '{print substr($0, 3)}' | awk '{print index($0,"/miniconda3/")}' | bc`
                set origcondalen=`echo $origcondapos+10`
                if ( $origcondapos == 0 ) then
                        set origcondapos=`cat $condainstdir/envs/$envdir/bin/pip | head -n 1 | awk '{print substr($0, 3)}' | awk '{print index($0,"/Python/")}' | bc`
                        set origcondalen=`echo $origcondapos+6`
                endif
                set origdir=`cat $condainstdir/envs/$envdir/bin/pip | head -n 1 | awk '{print substr($0, 3, '$origcondalen')}'`
                foreach envrepldir ( $envrepldirs )
			if ( ! -w "${condainstdir}/envs/${envdir}/${envrepldir}" ) then
				chmod og+w "${condainstdir}/envs/${envdir}/{envrepldir}"
			endif
                        if ( -w "${condainstdir}/envs/${envdir}/${envrepldir}" ) then
                                foreach fil (`ls --hide='~*' $condainstdir/envs/$envdir/$envrepldir`)
                                        set fildir=`file $condainstdir/envs/$envdir/$envrepldir/$fil | grep text | awk '{print $1}' | sed 's/.$//'`
                                        if ("$fildir" != "") then
                                                sed -i -- s,$origdir,$condainstdir,g $fildir
                                        endif
                                end
                        else
                                echo "Warning: $envrepldir is not writable or does not exist, therefore skipping $envrepldir."
                        endif
                end
        end
endif

# create Python_envs.txt file in OpendTect data directory
if ( $#argv >= 2 ) then
        if ( "$1" == "--odinstdir" ) then
                echo "Creating Python_envs.txt file..."
                set odinstdir=$2
                if ( -w "${odinstdir}/data" ) then
                        set fil="${odinstdir}/data/Python_envs.txt"
                        touch $fil
                        echo "dTect V6.6" > $fil
                        echo "Python" >> $fil
                        date >> $fil
                        echo "!" >> $fil
                        echo "File name: ${condainstdir}" >> $fil
                        echo "!" >> $fil
			if ( -w "${odinstdir}/v7/data" ) then
				cp $fil ${odinstdir}/v7/data
			endif
                else
                        echo "Warning: cannot properly setup Python environments connection with OpendTect."
                        echo "Reason: $odinstdir/data is not writable or does not exist."
                        echo "Will exit the setup now..."
                        exit 1
                endif
        endif
endif
