#!/bin/csh -f
#________________________________________________________________________
#
# Copyright:	(C) 1995-2023 dGB Beheer B.V.
# License:	https://dgbes.com/licensing
#________________________________________________________________________
#
# OpendTect environment initialization script
# Filled by 'install'

if ( ! $?DTECT_APPL ) then
    setenv DTECT_APPL "__INST_DIR__"
endif

if ( ! $?DTECT_DATA ) then
    if ( $?dGB_DATA ) then
	setenv DTECT_DATA "$dGB_DATA"
    else
	setenv DTECT_DATA "__DATA_DIR__"
    endif
endif

if ( "$DTECT_DATA" == "__none__" ) then
    unsetenv DTECT_DATA
endif

#-----------------------------------------------------
# General initialisation

source "${DTECT_APPL}/bin/mksethdir" "${DTECT_APPL}"

setenv LANG C.UTF-8
setenv LC_NUMERIC C.UTF-8

#-----------------------------------------------------
# PATH variables

if ( -d "${DTECT_APPL}/bin/${binsubdir}/Release" ) then
    set bindir="${DTECT_APPL}/bin/${binsubdir}/Release"
else if ( -d "${DTECT_APPL}/bin/${binsubdir}/Debug" ) then
    set bindir="${DTECT_APPL}/bin/${binsubdir}/Debug"
else
    exit 1
endif
set binpath="${bindir}:${DTECT_APPL}/bin"

setenv PATH "${binpath}:${PATH}"

if ( $HDIR == mac ) then

    if ( $?DYLD_LIBRARY_PATH ) then
	setenv OD_SYSTEM_LIBRARY_PATH "${DYLD_LIBRARY_PATH}"
    endif

else

    if ( $?LD_LIBRARY_PATH ) then
	setenv OD_SYSTEM_LIBRARY_PATH "${LD_LIBRARY_PATH}"
    endif

    if ( ! -e "${bindir}/../lib" ) then
	alias initwarn 'echo "${bindir}/../lib does not exist" ; echo "Please check your installation" ; echo "The application can however continue normally."'
	if ( $?LD_LIBRARY_PATH ) then
	    setenv LD_LIBRARY_PATH ${bindir}:${LD_LIBRARY_PATH}
	else
	    setenv LD_LIBRARY_PATH ${bindir}
	endif
    endif

    set ldconfig = "/sbin/ldconfig"

    # gcc library compatibility check
    set odsystemlibsdir = "${bindir}/systemlibs"
    if ( -e ${ldconfig} ) then
        set libstdc = `${ldconfig} -p | grep libstdc++.so.6 | grep 64 | tr ' ' '\n' | grep / | head -n 1`
	set cxxabiver = 1.3.12
	( which strings ) >& /dev/null
        if ( $status == 0 ) then
	    set gcccxxabiver = `strings "${libstdc}" | grep -m 1 CXXABI_${cxxabiver}`
	else
	    set gcccxxabiver = `grep -z CXXABI_${cxxabiver} ${libstdc}`
	endif
        if ( "${gcccxxabiver}" == "" ) then
	    if ( $?LD_LIBRARY_PATH ) then
		setenv LD_LIBRARY_PATH "${odsystemlibsdir}":${LD_LIBRARY_PATH}
	    else
		setenv LD_LIBRARY_PATH "${odsystemlibsdir}"
	    endif
        endif
    else
	if ( $?LD_LIBRARY_PATH ) then
	    setenv LD_LIBRARY_PATH "${odsystemlibsdir}":${LD_LIBRARY_PATH}
	else
	    setenv LD_LIBRARY_PATH "${odsystemlibsdir}"
	endif
    endif

    # png16 library compatibility check
    set odpng16libdir = "${bindir}/png16"
    if ( -e ${ldconfig} ) then
	set syslibpng16 = `${ldconfig} -p | grep libpng16.so.16 | grep 64 | tr ' ' '\n' | grep / | head -n 1`
    else
	set syslibpng16 = "/usr/lib64/libpng16.so.16"
    endif
    if ( ! -e "${syslibpng16}" && -e "${odpng16libdir}" ) then
	if ( $?LD_LIBRARY_PATH ) then
	    setenv LD_LIBRARY_PATH "${odpng16libdir}":${LD_LIBRARY_PATH}
	else
	    setenv LD_LIBRARY_PATH "${odpng16libdir}"
	endif
    endif

    # freetype library compatibility check
    set odfreetypelibdir = "${bindir}/freetype"
    if ( -e ${ldconfig} ) then
	set syslibfreetype = `${ldconfig} -p | grep libfreetype.so.6 | grep 64 | tr ' ' '\n' | grep / | head -n 1`
    else
	set syslibfreetype = "/usr/lib64/libfreetype.so.6"
    endif
    if ( ! -e "${syslibfreetype}" && -e "${odfreetypelibdir}" ) then
	if ( $?LD_LIBRARY_PATH ) then
	    setenv LD_LIBRARY_PATH "${odfreetypelibdir}":${LD_LIBRARY_PATH}
	else
	    setenv LD_LIBRARY_PATH "${odfreetypelibdir}"
	endif
    endif

    # fontconfig library compatibility check
    set odfontconfiglibdir = "${bindir}/fontconfig"
    if ( -e ${ldconfig} ) then
	set syslibfontconfig = `${ldconfig} -p | grep libfontconfig.so.1 | grep 64 | tr ' ' '\n' | grep / | head -n 1`
    else
	set syslibfontconfig = "/usr/lib64/libfontconfig.so.1"
    endif
    if ( ! -e "${syslibfontconfig}" && -e "${odfontconfiglibdir}" ) then
	if ( $?LD_LIBRARY_PATH ) then
	    setenv LD_LIBRARY_PATH "${odfontconfiglibdir}":${LD_LIBRARY_PATH}
	else
	    setenv LD_LIBRARY_PATH "${odfontconfiglibdir}"
	endif
    endif

    # kludge for Fedora 8
    setenv LIBXCB_ALLOW_SLOPPY_LOCK 1

endif


#-----------------------------------------------------
# Extra initialisations

set nonomatch=yes

cd "$DTECT_APPL"

foreach fil ( odinit.* )
    if ( -r "$fil" ) source "$fil"
end

if ( $?CHECK_GL ) then
    source "${DTECT_APPL}/bin/init_dtect_GL"
endif
