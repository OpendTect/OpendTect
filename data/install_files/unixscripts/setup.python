#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id$
#
# Sets up the python environments
#_______________________________________________________________________________

if ( "$1" == "--help" ) then
    echo "This script sets up the python environments"
    echo "You can run this script at any time"
    echo "If you add new python environments or move the installation directory, you have to run this script"
    exit 0
endif

# this fetches the original dir
set origcondapos=`cat bin/conda | head -n 1 | awk '{print substr($0, 3)}' | awk '{print index($0,"miniconda3")}' | bc`
set origcondalen=`echo $origcondapos+9`
set origdir=`cat bin/conda | head -n 1 | awk '{print substr($0, 3, '$origcondalen')}'`

# at this point currenct directory is installation directory.
set instdir=`pwd`

if ( ! -w "${instdir}" ) then
    echo "Warning: cannot properly setup Python environments."
    echo "Reason: ${instdir} is not writable."
    echo "Will exit the setup now, Python environments are not set up propertly"
    exit 1
endif

if ( ! -w "${instdir}"/envs ) then
    echo "Warning: cannot properly setup Python environments."
    echo "Reason: ${instdir}/envs is not writable."
    echo "Will exit the setup now, Python environments are not set up propertly"
    exit 1
endif

# gets environment dirs and sets which dirs to walk through
set envdirs=`ls $instdir/envs/`
set envrepldirs='bin conda-meta etc/fonts lib/python3.7/site-packages ssl/misc'
set instrepldirs='bin condabin conda-meta etc/fish/conf.d etc/profile.d lib lib/itcl4.1.1 lib/pkgconfig lib/python3.7 lib/python3.7/config-3.7m-x86_64-linux-gnu shell/condabin ssl/misc'

# replace paths in miniconda3 dir
echo "Setting up miniconda3 dir..."
foreach instrepldir ( $instrepldirs)
        foreach fil (`ls --hide='~*' $instdir/$instrepldir`)
                set fildir=`file $instdir/$instrepldir/$fil | grep ASCII | awk '{print $1}' | sed 's/.$//'`
                if ("$fildir" != "") then
                        sed -i -- s,$origdir,$instdir,g $fildir
                endif
        end
end

# replace paths in environment dirs
if ("$envdirs" != "") then
	foreach envdir ( $envdirs )
		echo "Setting up $envdir..."
		set origcondapos=`cat $instdir/envs/$envdir/bin/pip | head -n 1 | awk '{print substr($0, 3)}' | awk '{print index($0,"miniconda3")}' | bc`
		set origcondalen=`echo $origcondapos+9`
		set origdir=`cat $instdir/envs/$envdir/bin/pip | head -n 1 | awk '{print substr($0, 3, '$origcondalen')}'`
		foreach envrepldir ( $envrepldirs )
			foreach fil (`ls --hide='~*' $instdir/envs/$envdir/$envrepldir`)
				set fildir=`file $instdir/envs/$envdir/$envrepldir/$fil | grep ASCII | awk '{print $1}' | sed 's/.$//'`
				if ("$fildir" != "") then
					sed -i -- s,$origdir,$instdir,g $fildir
				endif
			end
		end
	end
endif

