#________________________________________________________________________
#
# Copyright:    dGB Beheer B.V.
# License:      https://dgbes.com/index.php/licensing
#________________________________________________________________________
#
# CMake script to define base package variables
#

#OpendTect modules and plugins
set( LIBLIST @OD_CORE_MODULE_NAMES_od@ )

set( PLUGINS @OD_PLUGINS@ )
set( EXCLUDE_PLUGINS Hello uiHello Tut uiTut uiDPSDemo uiQtApp
    uiTutMadagascar uiCrashMe uiCOLOP )
list( REMOVE_ITEM PLUGINS ${EXCLUDE_PLUGINS} )

set( EXECLIST @OD_BASE_EXECUTABLE@ )
set( EXCLUDE_EXECS od_layouttest od_screentest od_sysadmmain )
list( REMOVE_ITEM EXECLIST ${EXCLUDE_EXECS} )
if ( WIN32 )
    list( APPEND EXECLIST "od_main_debug.bat" )
endif()

set( THIRDPARTY_LIBS "${OD_THIRDPARTY_LIBS}" )
set( THIRDPARTY_TARGETS "${OD_THIRDPARTY_TARGETS}" )
set( QTPLUGINS "${OD_QTPLUGINS}" )

#No need to include shell scripts in windows base package.
if( WIN32 )
    set( ODSCRIPTS od_external.bat )
else()
    file( GLOB ODSCRIPTS RELATIVE "${CMAKE_INSTALL_PREFIX}/bin"
	    "${CMAKE_INSTALL_PREFIX}/bin/od_*" )
    list( REMOVE_ITEM ODSCRIPTS od_cr_dev_env.bat od_external.bat od_main_debug.bat )
    list( APPEND ODSCRIPTS init_dtect_GL mksethdir )
    if ( APPLE )
	list( APPEND ODSCRIPTS macterm.in )
    endif()
    set( SPECFILES .exec_prog .init_dtect .init_dtect_user install .lic_inst_common
		   .lic_start_common mk_datadir .start_dtect setup.od )
    if ( APPLE )
	list( APPEND SPECFILES od.icns )
    endif()
endif()

if ( APPLE )
    list( APPEND OTHERFILES "${COPYFROMDIR}/../Info.plist" )
    list( APPEND OTHERFILESDEST "${COPYTODIR}/../" )
endif()

set( LMUTIL "${COPYFROMSCRIPTSDIR}/${OD_PLFSUBDIR}/lm.dgb/lmutil" )
if ( WIN32 )
    set( LMUTIL "${LMUTIL}.exe" )
endif()
list( APPEND OTHERFILES "${LMUTIL}" )
list( APPEND OTHERFILESDEST "${COPYTOSCRIPTSDIR}/${OD_PLFSUBDIR}/lm.dgb" )

if ( WIN32 )
    set( ML_FW_RULENMS "${COPYFROMDATADIR}/Firewall/od_main.txt"
		"${COPYFROMDATADIR}/Firewall/od_remoteservice.txt"
		"${COPYFROMDATADIR}/Firewall/od_SeisMMBatch.txt" )
    foreach( ML_FW_RULENM ${ML_FW_RULENMS} )
        list( APPEND OTHERFILES "${ML_FW_RULENM}" )
        list( APPEND OTHERFILESDEST "${COPYTODATADIR}/Firewall" )
    endforeach()
endif()

set( SURV_PROVS "${COPYFROMDATADIR}/SurveyProviders/uiSEGYTools.txt"
		"${COPYFROMDATADIR}/SurveyProviders/uiWell.txt" )
foreach( SURV_PROV ${SURV_PROVS} )
    list( APPEND OTHERFILES "${SURV_PROV}" )
    list( APPEND OTHERFILESDEST "${COPYTODATADIR}/SurveyProviders" )
endforeach()

if ( MATLABLink IN_LIST PLUGINS )
    set( EXECDIRS MATLAB )
    list( APPEND SPECFILES odinit.matlab )
endif()

set( LEGALOPTS zlib openssl sqlite proj hdf5 )
set( LEGALTRGTS ZLIB::ZLIB OpenSSL::SSL SQLite::SQLite3 PROJ::proj hdf5::hdf5-shared )
foreach( opt trgt IN ZIP_LISTS LEGALOPTS LEGALTRGTS )
    if ( ${trgt} IN_LIST OD_THIRDPARTY_TARGETS )
        list( APPEND LEGALLIST ${opt} )
    endif()
endforeach()

set( PYTHONDIR odpy;odbind;safety;marshmallow )

set( PACK base )
