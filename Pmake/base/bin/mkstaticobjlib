#!/bin/csh -f
#
# $Id: mkstaticobjlib,v 1.1 2007/04/12 15:49:54 cvsbert Exp $
# This script creates a static object lib from a number of input libraries
#_______________________________________________________________________________

if ( $#argv < 3 ) then
	echo "Usage: $0 input_lib [input_lib ...] --output output_lib"
	exit 1
endif

set inplibs=""
foreach lib ( $* )

    if ( "$lib" == "--output" ) goto inplibs_done

    if ( ! -r "$lib" ) then
	    echo "$0 : Input lib $lib cannot be accessed"
	    exit 1
    endif

    set inplibs="$inplibs $lib"
    shift

end
inplibs_done:
shift

set outlib=$1
shift

set tmpdir=`$PMAKE/bin/get_temp_file_name`

if ( ! -d $tmpdir ) mkdir $tmpdir
if ( ! -d $tmpdir ) then
    echo "$0 : Cannot create $tmpdir"
    exit 1
endif

cd $tmpdir

foreach lib ( $inplibs )
    if ( $HDIR == "mac") then
	ar x $lib `ar t $lib | grep -v __.SYMDEF `
    else
	ar x $lib `ar t $lib `
    endif
end

ar qv $outlib *.o
set exitstat=$status
rm -rf $tmpdir

exit $exitstat
