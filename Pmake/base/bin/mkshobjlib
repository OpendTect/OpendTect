#!/bin/csh -f
#
# $Id: mkshobjlib,v 1.12 2009/03/06 13:52:28 cvskris Exp $
# This script creates a shared object lib from a number of input libraries
#_______________________________________________________________________________

set dolmstrip=no
if ( "$1" == "--lmstrip" ) then
    set dolmstrip=yes
    shift
endif


if ( $#argv < 3 ) then
	echo "Usage: $0 input_lib [input_lib ...] --output output_shlib linker_and_flags"
	exit 1
endif

set inplibs=""
foreach lib ( $* )

    if ( "$lib" == "--output" ) goto inplibs_done

    if ( ! -r "$lib" ) then
	    echo "$0 : Input lib $lib cannot be accessed"
	    exit 1
    endif

    set inplibs="$inplibs $lib"
    shift

end
inplibs_done:
shift

set outlib=$1
shift

if ( $HDIR == "win") then

    $* -o $outlib
    set exitstat=$status

else

    set tmpdir=`$PMAKE/bin/get_temp_file_name`

    if ( ! -d $tmpdir ) mkdir $tmpdir
    if ( ! -d $tmpdir ) then
	    echo "$0 : Cannot create $tmpdir"
	    exit 1
    endif

    cd $tmpdir

    foreach lib ( $inplibs )
	if ( $HDIR == "mac") then
		ar x $lib `ar t $lib | grep -v __.SYMDEF `
	else
		ar x $lib `ar t $lib `
	endif
    end

    $* *.o -o $outlib
    set exitstat=$status

    #Generate debug-info on mac
    if ( $HDIR == "mac" && $DEBUG != "no" ) then
	dsymutil $outlib
    endif
	
    rm -rf $tmpdir

endif

if ( $dolmstrip == yes ) then
	$WORK/bin/do_lmstrip $outlib
endif

exit $exitstat
