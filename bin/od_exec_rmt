#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id$
#
# module remote startup script
#_______________________________________________________________________________

if ( $?DTECT_SCRIPT_VERBOSE ) then
    echo "$0  ++++++"
    echo "args: $*"
    set verbose=yes
    set echo=on
endif

if ( $?DTECT_SCRIPT_DEBUG ) then
    echo "od_exec_rmt $*"
endif

if ( ! $?DTECT_APPL ) then
	echo "Error : environment not initialized."
	exit 1
endif

if ( $#argv < 1 ) then
    echo "Usage: $0 remote_host [--rexec remote_shell_command]"
    echo "       [--with-dtect-appl <DTECT_APPL on remote host>"
    echo "       [--with-dtect-data <DTECT_DATA on remote host>"
    echo "       [--with-remoteuser < user on remote host>"
    echo "       [--with-remotepass < pass on remote host>"
    echo "       [--with-data-host <win32 host were data is stored>"
    echo "       [--with-data-drive <win32 data drive letter>"
    echo "       [--with-data-share <win32 share name where data is stored>"
    echo "       [--with-local-file-base <local base file name>"
    echo "       [--with-remote-file-base <remote base file name>"
    echo "       [--transfervars <list of environment vars to transfer> -+-+-"
    echo "       [--iswin]"
endif

set remotehost=$1
shift

set rshcomm=rsh
set transfvars=""
set remoteuser=""
set remotepass=""
if ( $?DTECT_REMOTE_PASS ) then
    set remotepass="$DTECT_REMOTE_PASS"
endif

set dtectappl="$DTECT_APPL"
if ( $?DTECT_DATA ) then
    set dtectdata="$DTECT_DATA"
endif

set datahost=""
set datadrive=""
set datashare=""
set datavars=""
set iswin=0

next_arg:

if ( "$1" == "--rexec" ) then
	set rshcomm=$2
	shift ; shift
	goto next_arg
else if ( $1 == "--remoteuser" ) then
        set remoteuser=" -l $2"
        shift; shift
        goto next_arg
else if ( $1 == "--remotepass" ) then
        set remotepass="$2"
	set datavars="${datavars} --remotepass $2"
        shift; shift
        goto next_arg
else if ( $1 == "--data-host" ) then
        set datahost="$2"
	set datavars="${datavars} --data-host $2"
        shift; shift
        goto next_arg
else if ( $1 == "--data-drive" ) then
        set datadrive="$2"
	set datavars="${datavars} --data-drive $2"
        shift; shift
        goto next_arg
else if ( $1 == "--data-share" ) then
        set datashare="$2"
	set datavars="${datavars} --data-share $2"
        shift; shift
        goto next_arg
else if ( $1 == "--iswin" ) then
        set iswin=1
        shift
        goto next_arg
else if ( $1 == "--with-dtect-appl" ) then
        set dtectappl="$2"
        shift; shift
        goto next_arg
else if ( $1 == "--with-dtect-data" ) then
        set dtectdata="$2"
        shift; shift
        goto next_arg
else if ( $1 == "--with-local-file-base" ) then
        set locfilebase="$2"
        shift; shift
        goto next_arg
else if ( $1 == "--with-remote-file-base" ) then
        set remfilebase="$2"
        shift; shift
        goto next_arg
else if ( "$1" == "--transfervars" ) then
    next_var:
	shift
	if ( "$1" == "-+-+-" ) then
	    shift
	    goto next_arg
	endif

	set transfvars="$transfvars $1"
	goto next_var
endif

set transf="setenv DTECT_APPL -=-${dtectappl}-=-"

if ( $?dtectdata ) then
    set transf="${transf} +-+  setenv DTECT_DATA -=-${dtectdata}-=-"
endif

if ( $?DTECT_USER ) then
	set transf="${transf} +-+ setenv DTECT_USER ${DTECT_USER}"
endif

if ( $?LM_LICENSE_FILE ) then
	set transf="${transf} +-+ setenv LM_LICENSE_FILE -=-${LM_LICENSE_FILE}-=-"
endif

if ( $?DTECT_DEBUG ) then
    set transf="${transf} +-+ setenv DTECT_DEBUG ${DTECT_DEBUG}"
endif
if ( $?DEBUG ) then
    set transf="${transf} +-+ setenv DEBUG ${DEBUG}"
endif

if ( $?DTECT_SCRIPT_DEBUG ) then
    set transf="${transf} +-+ setenv DTECT_SCRIPT_DEBUG ${DTECT_SCRIPT_DEBUG}"
endif
if ( $?DTECT_SCRIPT_VERBOSE ) then
    set transf="${transf} +-+ setenv DTECT_SCRIPT_VERBOSE ${DTECT_SCRIPT_VERBOSE}"
endif
if ( $?OD_USER_PLUGIN_DIR ) then
    set transf="${transf} +-+ setenv OD_USER_PLUGIN_DIR ${OD_USER_PLUGIN_DIR}"
endif
if ( $?OD_APPL_PLUGIN_DIR ) then
    set transf="${transf} +-+ setenv OD_APPL_PLUGIN_DIR ${OD_APPL_PLUGIN_DIR}"
endif
if ( $?OD_SHOW_PLUGIN_LOAD ) then
    set transf="${transf} +-+ setenv OD_SHOW_PLUGIN_LOAD ${OD_SHOW_PLUGIN_LOAD}"
endif

if ( $?OD_REMOTE_DISPLAY ) then
	set transf="${transf} +-+ setenv DISPLAY $OD_REMOTE_DISPLAY"
else if ( $?DISPLAY ) then
	set dnm=`echo $DISPLAY|sed 's/://'| awk '{print $1}'`
	if ( $dnm == local || $dnm == unix || $dnm == 0 || $dnm == "0.0" ) then
		set hona=`uname -a|awk '{print $2}'`
		set nr=`echo $DISPLAY|sed 's/://'| awk '{print $2}'`
		if ( $nr == "" ) set nr=0
		setenv DISPLAY ${hona}":"${nr}.0
	else if ( $dnm == "1" || $dnm == "1.0" ) then
		set hona=`uname -a|awk '{print $2}'`
		setenv DISPLAY ${hona}":1.0"
	endif
	set transf="${transf} +-+ setenv DISPLAY $DISPLAY"
endif

foreach var ( $transfvars )
    set dollarvar=`echo "$ ${var}" | sed "s/ //"`
    set varvalue=`eval "echo $dollarvar"`
    if ( $varvalue != "" ) then
	set transf="${transf} +-+ setenv $var $varvalue"
    endif
end

set transf=" $datavars $transf +-+-+ $*"

set do_exec=`echo "${dtectappl}/bin/od_do_rmt" | sed -e "s% %\\ %g"`


#if ( $iswin == 1 ) then
if ( ${rshcomm} == "rcmd" && $HDIR == "win" ) then

    set loctransffile="${locfilebase}.vars"
    set remtransffile="${remfilebase}.vars"

    echo "${transf}" > "${loctransffile}"

    set transf=`echo "${remtransffile}" | sed -e 's% %#-#%g'`

    set usernm=""
    if ( $?USERNAME ) then
	set usernm="$USERNAME"
    endif
    if ( $?user ) then
	set usernm="$user"
    endif

    set transf="$transf $datahost $datadrive $datashare $usernm $remotepass"

    if ( ${rshcomm} == "rcmd" && $HDIR == "win" ) then
	set do_exec=`echo '%DTECT_WINAPPL%\\bin\\od_dormt.bat' \
		     | sed -e "s% %\\ %g" -e 's%^%""%g' -e 's%$%""%g'`

	setenv PRESCRIPT `"${DTECT_APPL}/bin/${HDIR}/SearchODFile" \
	    od_prepare.bat | sed -e "s%\\%\\\\%g" -e 's%^%"%g' -e 's%$%"%g'`


	if ( $?DTECT_SCRIPT_DEBUG) then
	    echo cmd /c \
		"%PRESCRIPT% $remotehost _none_ _none_ $usernm $remotepass"
	endif

        cmd /c "%PRESCRIPT% $remotehost _none_ _none_ $usernm $remotepass"

	set remotehost="\\${remotehost}"

	if ( $?DTECT_SCRIPT_DEBUG ) then
	    echo ${rshcomm} ${remoteuser} "${remotehost}" ${do_exec} $transf
	    ${rshcomm} ${remoteuser} "${remotehost}" ${do_exec} $transf
	    exit $status 
	endif

	${rshcomm} ${remoteuser} "${remotehost}" ${do_exec} $transf >& /dev/null
	exit $status 

    else
	set do_exec=`echo "${dtectappl}/bin/od_dormt.bat" | sed -e "s% %\\ %g"`
    endif
endif


if ( $?DTECT_SCRIPT_DEBUG ) then
    echo ${rshcomm} ${remoteuser} "${remotehost}" "${do_exec} $transf"
endif

${rshcomm} ${remoteuser} "${remotehost}" "${do_exec} $transf"
exit 0
