#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id$
#
# module startup script
#_______________________________________________________________________________


if ( $?DTECT_SCRIPT_VERBOSE ) then
    echo "$0  ++++++"
    echo "args: $*"
    set verbose=yes
    set echo=on
endif

if ( ! $?DTECT_APPL ) then
    echo "$0 : Error : environment not initialized."
    exit 1
endif

if ( $?COMSPEC ) then
    setenv HDIR win
    set binsubdir=win
else
    eval `"$DTECT_APPL/bin/mksethdir"`
endif
if ( $binsubdir == luxia64 ) then
	echo "Warning: IA64 Linux not supported. Trying x32 emulation."
	set binsubdir=lux32
endif

set bindir="$DTECT_APPL/bin/$binsubdir"
set nicecmd=""
set debug=no
set extrascript=""
set xtermcmd=""
set indosbox=no
set askclosebox=no
set issys=no
set background=no

next_arg:

if ( "$1" == "--sys" ) then

    set issys=yes
    shift
    goto next_arg

else if ( "$1" == "--inbg" ) then

    set background=yes
    shift
    goto next_arg

else if ( "$1" == "--debug" ) then
    set bindir=$bindir/Debug
    echo $bindir
    shift
    goto next_arg

else if ( "$1" == "--release" ) then
    set bindir=$bindir/Release
    echo $bindir
    shift
    goto next_arg

else if ( "$1" == "--nice" ) then

    set niceness=$2
    set nicecmd=/usr/bin/nice
    if ( -x /bin/nice ) set nicecmd=/bin/nice
    if ( ! -x $nicecmd || $?COMSPEC ) then
	set nicecmd=""
    else
	set nicecmd="$nicecmd -$2"
    endif

    shift ; shift
    goto next_arg

else if ( "$1" == "--prescript" ) then

    set extrascript="$2"
    if ( ! -e $extrascript ) then
	if ( -e "$DTECT_APPL/bin/$extrascript" ) then
	    set extrascript="$DTECT_APPL/bin/$extrascript"
	else if ( -e "$bindir/$extrascript" ) then
	    set extrascript="$bindir/$extrascript"
	endif
    endif

    shift ; shift
    goto next_arg

else if ( "$1" == "--inxterm" || "$1" == "--inxterm+askclose" ) then

    if ( $HDIR == win ) then

        set indosbox=yes
        if ( "$1" == "--inxterm+askclose" ) then
            set askclosebox=yes
        endif

    else

	set xtermcmdarg=""
	if ( "$1" == "--inxterm+askclose" ) then
	    set xtermcmdarg = "--askclose"
	endif

	set xtermpath=xterm
	which xterm >&! /dev/null
	set result=$status
	if ( $result == 0 ) set xtermpath=`which xterm` >&! /dev/null

	if ( ! -x $xtermpath ) set result=1
	
	if ( $result != 0 ) then
	    set xtermpath=/usr/bin/X11/xterm
	    if ( $HDIR == sun5 ) then
	        set xtermpath=/usr/openwin/bin/xterm
		if ( ! -x $xtermpath ) set xtermpath=/usr/bin/xterm
	    endif
	    if ( $HDIR == mac ) set xtermpath="$DTECT_APPL/bin/mac_term"
	endif

	if ( $HDIR == mac ) then
	    set xtermcmd="$DTECT_APPL/bin/mac_term $xtermcmdarg"
	else
	    set xtermcmd="$xtermpath -bg lightyellow -fg black -geometry 80x25 -e $DTECT_APPL/bin/od_xterm_exec $DTECT_APPL $xtermcmdarg"
	endif

    endif

    shift
    goto next_arg

endif


if ( ! -d "$bindir" ) then
    echo "Fatal error: Cannot start program because directory:"
    echo "$bindir"
    echo "Does not exist."
    exit 1
endif

set prog="$1"

if ( ! -e "$bindir/$prog" ) then
    if ( $?DEBUG && -e "$bindir/G/$prog" ) then
	set bindir="$bindir/G"
    else if ( -e "$bindir/O/$prog" ) then
	set bindir="$bindir/O"
    endif
    if ( ! -e "$bindir/$prog" ) then
    	if ( $binsubdir == lux64 && -e "$DTECT_APPL/bin/lux32/$prog" ) then
	    set binsubdir=lux32
	    set bindir="$DTECT_APPL/bin/$binsubdir"
	endif
    endif
endif

setenv XAPPLRESDIR	"$DTECT_APPL/data"
setenv PATH		"${bindir}:${PATH}"

if ( $HDIR == mac ) then
    if ( $?DYLD_LIBRARY_PATH ) then
	setenv DYLD_LIBRARY_PATH	"${bindir}/so:${DYLD_LIBRARY_PATH}"
    else
	setenv DYLD_LIBRARY_PATH	"${bindir}/so:/lib:/usr/lib"
    endif
else
    if ( $?LD_LIBRARY_PATH ) then
	setenv LD_LIBRARY_PATH	"${bindir}/so:${LD_LIBRARY_PATH}"
    else
	setenv LD_LIBRARY_PATH	"${bindir}/so:/lib:/usr/lib"
    endif
endif

if ( $HDIR == sgi ) then
    if ( $?LD_LIBRARYN32_PATH ) then
        setenv LD_LIBRARYN32_PATH "${bindir}/so:${LD_LIBRARYN32_PATH}"
    else
        setenv LD_LIBRARYN32_PATH "${bindir}/so:/lib:/usr/lib"
    endif
endif

unsetenv QTDIR

if ( $extrascript != "" ) then
    source $extrascript
endif


# win32 only - when in dos box - handle separately
if ( $indosbox == yes ) then

    if ( $issys == yes ) then
	set execfile=`which $prog`
    else
	set execfile="$bindir/$prog"
    endif

    shift

    set tempbatfile="/tmp/odexec$$.bat"

    if ( $?TEMP ) then
        set tempbatfile=`cygpath -u "$TEMP\odexec$$.bat"`
    else if ( $?TMP ) then
        set tempbatfile=`cygpath -u "$TMP\odexec$$.bat"`
    else if ( $?DTECT_MYDOCUMENTS_DIR ) then
        set tempbatfile=`cygpath -u "$DTECT_MYDOCUMENTS_DIR\odexec$$.bat"`
    else if ( $?USERPROFILE ) then
        set tempbatfile=`cygpath -u "$USERPROFILE\odexec$$.bat"`
    endif

    if ( -e "${execfile}.exe" ) then
	setenv executable `cygpath -w "${execfile}.exe" | sed -e's%\\%\\\\%g'`
    else
	setenv executable `cygpath -w "$execfile" | sed -e's%\\%\\\\%g'`
    endif

    setenv args "$nicecmd -@-$executable-@- "
    foreach arg ( $* )
        setenv args "$args -@-$arg-@-"
    end

    echo "$args" | sed -e 's/-@-/"/g' -e 's/%/ /g' > "$tempbatfile"
    if ( $askclosebox == yes ) then
        echo "pause" >> "$tempbatfile"
    endif

    echo "exit" >> "$tempbatfile"

    setenv batfile `cygpath -ws "${tempbatfile}"`

    echo cmd /E:off /c start "$batfile"
    cmd /E:off /c start "$batfile"

    exit $status

endif

# Further only UNIX or not in dos box

set allargs=""

more_args:

if ( "$1" != "" ) then

    set arg=`echo "$1" | sed 's/ /%/g'`
    if ( "$allargs" == "" ) then
	set allargs="$arg"
    else
	set allargs="$allargs $arg"
    endif

    shift
    goto more_args

endif

set exitcode = 0
if ( $issys == yes ) then

    if ( $?DTECT_SCRIPT_DEBUG ) echo "$0 : $xtermcmd $nicecmd $allargs"
    if ( $background == yes ) then
	$xtermcmd $nicecmd $allargs &
	set exitcode = $status
    else
	$xtermcmd $nicecmd $allargs 
	set exitcode = $status
    endif
else
    if ( $?DTECT_SCRIPT_DEBUG ) then
	if ( $?COMSPEC ) then
	    echo "$0 : analyse dll deps"
	    cygcheck `cygpath -w "${bindir}/${prog}.exe"` 
	endif

	echo "$0 : $xtermcmd $nicecmd $bindir/$allargs"
    endif

    if ( $?DTECT_PRELOAD ) then
    	setenv LD_PRELOAD $DTECT_PRELOAD
    endif

    if ( $background == yes ) then
	$xtermcmd $nicecmd "$bindir"/$allargs &
	set exitcode = $status
    else
	$xtermcmd $nicecmd "$bindir"/$allargs
	set exitcode = $status
    endif
endif

exit $exitcode
