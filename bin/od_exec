#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id: od_exec,v 1.36 2004-11-22 09:55:36 arend Exp $
#
# module startup script
#_______________________________________________________________________________


if ( $?DTECT_DEBUG ) set verbose=yes

if ( ! $?DTECT_APPL ) then
    echo "$0 : Error : environment not initialized."
    exit 1
endif

if ( $?COMSPEC ) then
    setenv HDIR win
    set binsubdir=win
else
    eval `"$DTECT_APPL/bin/mksethdir"`
endif

set bindir="$DTECT_APPL/bin/$binsubdir"
set nicecmd=""
set extrascript=""
set extraargs=""
set xtermcmd=""
set indosbox=no
set askclosebox=no
set issys=no
set background=no

next_arg:

if ( "$1" == "--sys" ) then

    set issys=yes
    shift
    goto next_arg

else if ( "$1" == "--inbg" ) then

    set background=yes
    shift
    goto next_arg

else if ( "$1" == "--nice" ) then

    set niceness=$2
    set nicecmd=/usr/bin/nice
    if ( -x /bin/nice ) set nicecmd=/bin/nice
    if ( ! -x $nicecmd ) then
	set nicecmd=""
    else
	set nicecmd="$nicecmd -$2"
    endif

    shift ; shift
    goto next_arg

else if ( "$1" == "--prescript" ) then

    set extrascript="$2"
    if ( ! -e $extrascript ) then
	if ( -e "$DTECT_APPL/bin/$extrascript" ) then
	    set extrascript="$DTECT_APPL/bin/$extrascript"
	else if ( -e "$bindir/$extrascript" ) then
	    set extrascript="$bindir/$extrascript"
	endif
    endif

    shift ; shift
    goto next_arg

else if ( "$1" == "--inxterm" || "$1" == "--inxterm+askclose" ) then

    if ( $HDIR == win ) then

        set indosbox=yes
        if ( "$1" == "--inxterm+askclose" ) then
            set askclosebox=yes
        endif

    else

	set xtermcmdarg=""
	if ( "$1" == "--inxterm+askclose" ) then
	    set xtermcmdarg = "--askclose"
	endif

	set xtermpath=xterm
	which xterm >&! /dev/null
	set result=$status
	if ( $result == 0 ) set xtermpath=`which xterm` >&! /dev/null

	if ( ! -x $xtermpath ) set result=1
	
	if ( $result != 0 ) then
	    set xtermpath=/usr/bin/X11/xterm
	    if ( $HDIR == sun5 ) then
	        set xtermpath=/usr/openwin/bin/xterm
		if ( ! -x $xtermpath ) set xtermpath=/usr/bin/xterm
	    endif
	    if ( $HDIR == mac ) set xtermpath="$DTECT_APPL/bin/mac_term"
	endif

	if ( $HDIR == mac ) then
	    set xtermcmd="$DTECT_APPL/bin/mac_term $xtermcmdarg"
	else
	    set xtermcmd="$xtermpath -bg lightyellow -fg black -geometry 80x25 -e $DTECT_APPL/bin/od_xterm_exec $DTECT_APPL $xtermcmdarg"
	endif

    endif

    shift
    goto next_arg

endif


if ( ! -d "$bindir" ) then
    echo "Fatal error: Cannot start program because directory:"
    echo "$bindir"
    echo "Does not exist."
    exit 1
endif

setenv XAPPLRESDIR	"$DTECT_APPL/data"
setenv PATH		"${bindir}:${PATH}"

if ( $HDIR == mac ) then
    if ( $?DYLD_LIBRARY_PATH ) then
	setenv DYLD_LIBRARY_PATH	"${bindir}/so:${DYLD_LIBRARY_PATH}"
    else
	setenv DYLD_LIBRARY_PATH	"${bindir}/so:/lib:/usr/lib"
    endif
else
    if ( $?LD_LIBRARY_PATH ) then
	setenv LD_LIBRARY_PATH	"${bindir}/so:${LD_LIBRARY_PATH}"
    else
	setenv LD_LIBRARY_PATH	"${bindir}/so:/lib:/usr/lib"
    endif
endif

if ( $HDIR == sgi ) then
    if ( $?LD_LIBRARYN32_PATH ) then
        setenv LD_LIBRARYN32_PATH "${bindir}/so:${LD_LIBRARYN32_PATH}"
    else
        setenv LD_LIBRARYN32_PATH "${bindir}/so:/lib:/usr/lib"
    endif
endif

unsetenv QTDIR

if ( $extrascript != "" ) then
    source $extrascript
endif


# win32 only
if ( $indosbox == yes ) then

    if ( $issys == yes ) then
	set execfile=`which $1`
    else
	set execfile="$bindir/$1"
    endif

    shift

    set tempbatfile="/tmp/odexec$$.bat"

    if ( $?TEMP ) then
        set tempbatfile=`cygpath -u "$TEMP\odexec$$.bat"`
    else if ( $?TMP ) then
        set tempbatfile=`cygpath -u "$TMP\odexec$$.bat"`
    else if ( $?DTECT_MYDOCUMENTS_DIR ) then
        set tempbatfile=`cygpath -u "$DTECT_MYDOCUMENTS_DIR\odexec$$.bat"`
    else if ( $?USERPROFILE ) then
        set tempbatfile=`cygpath -u "$USERPROFILE\odexec$$.bat"`
    endif

    if ( -e "${execfile}.exe" ) then
	setenv executable `cygpath -ws "${execfile}.exe" | sed -e's%\\%\\\\%g'`
    else
	setenv executable `cygpath -ws "$execfile" | sed -e's%\\%\\\\%g'`
    endif

    setenv args "$nicecmd @$executable@ "
    foreach arg( $* )
        setenv args "$args @$arg@"
    end
    setenv args "$args $extraargs"

    echo "$args" | sed -e's/@/"/g' -e's/%/ /g' > "$tempbatfile"
    if ( $askclosebox == yes ) then
        echo "pause" >> "$tempbatfile"
    endif

    echo "exit" >> "$tempbatfile"

    setenv batfile `cygpath -ws "${tempbatfile}"`

    echo cmd /E:off /c start "$batfile"
    cmd /E:off /c start "$batfile"

    ## TODO : remove temporary .bat file
    #\rm $tempbatfile

    exit $status

endif


if ( $issys == yes ) then
    if ( $?DTECT_DEBUG ) echo "$0 : $xtermcmd $nicecmd $* $extraargs"
    if ( $background == yes ) then
	$xtermcmd $nicecmd $* $extraargs &
    else
	$xtermcmd $nicecmd $* $extraargs
    endif
else
    if ( $?DTECT_DEBUG ) echo "$0 : $xtermcmd $nicecmd $bindir/$* $extraargs"
    if ( $background == yes ) then
	$xtermcmd $nicecmd "$bindir"/$* $extraargs &
    else
	$xtermcmd $nicecmd "$bindir"/$* $extraargs
    endif
endif

exit $status
