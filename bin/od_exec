#!/bin/csh -f
#________________________________________________________________________
#
# Copyright:    (C) 1995-2022 dGB Beheer B.V.
# License:      https://dgbes.com/licensing
#________________________________________________________________________
#
# module startup script
#

if ( $?DTECT_SCRIPT_VERBOSE ) then
    echo "$0  ++++++"
    echo "args: $*"
    set verbose=yes
    set echo=on
endif

set SCRIPTDIR = `dirname "$0"`
set DTECT_APPL  = `dirname "${SCRIPTDIR}"`

source $DTECT_APPL/bin/mksethdir ${DTECT_APPL}
if ( $binsubdir == luxia64 ) then
	echo "Warning: IA64 Linux not supported. Trying x64 emulation."
	set binsubdir=lux64
endif

set bindir="$DTECT_APPL/bin/$binsubdir"
set nicecmd=""
set debug=no
set extrascript=""
set xtermcmd=""
set indosbox=no
set askclosebox=no
set issys=no
set background=no

next_arg:

if ( "$1" == "--sys" ) then

    set issys=yes
    shift
    goto next_arg

else if ( "$1" == "--inbg" ) then

    set background=yes
    shift
    goto next_arg

else if ( "$1" == "--debug" ) then
    set bindir="$bindir/Debug"
    echo "Using debug bindir: $bindir"
    shift
    goto next_arg

else if ( "$1" == "--release" ) then
    set bindir="$bindir/Release"
    echo $bindir
    shift
    goto next_arg

else if ( "$1" == "--nice" ) then

    set niceness=$2
    set nicecmd=/usr/bin/nice
    if ( -x /bin/nice ) set nicecmd=/bin/nice
    if ( ! -x $nicecmd || $?COMSPEC ) then
	set nicecmd=""
    else
	set nicecmd="$nicecmd -$2"
    endif

    shift ; shift
    goto next_arg

else if ( "$1" == "--prescript" ) then

    set extrascript="$2"
    if ( ! -e $extrascript ) then
	if ( -e "$DTECT_APPL/bin/$extrascript" ) then
	    set extrascript="$DTECT_APPL/bin/$extrascript"
	else if ( -e "$bindir/$extrascript" ) then
	    set extrascript="$bindir/$extrascript"
	endif
    endif

    shift ; shift
    goto next_arg

else if ( "$1" == "--inxterm" || "$1" == "--inxterm+askclose" ) then

    if ( $HDIR == win ) then

        set indosbox=yes
        if ( "$1" == "--inxterm+askclose" ) then
            set askclosebox=yes
        endif

    else

	set xtermcmdarg=""
	if ( "$1" == "--inxterm+askclose" ) then
	    set xtermcmdarg = "--askclose"
	endif

	set xtermpath=xterm
	which xterm >&! /dev/null
	set result=$status
	if ( $result == 0 ) set xtermpath=`which xterm` >&! /dev/null

	if ( ! -x $xtermpath ) set result=1
	
	if ( $result != 0 ) then
	    set xtermpath=/usr/bin/X11/xterm
	    if ( $HDIR == sun5 ) then
	        set xtermpath=/usr/openwin/bin/xterm
		if ( ! -x $xtermpath ) set xtermpath=/usr/bin/xterm
	    endif
	    if ( $HDIR == mac ) set xtermpath="$DTECT_APPL/bin/mac_term"
	endif

	if ( $HDIR == mac ) then
	    set xtermcmd="$DTECT_APPL/bin/mac_term $xtermcmdarg"
	else
	    set xtermcmd="$xtermpath -bg lightyellow -fg black -geometry 80x25 -e $DTECT_APPL/bin/od_xterm_exec $DTECT_APPL $xtermcmdarg"
	endif

    endif

    shift
    goto next_arg

endif

if ( ! -d "$bindir" ) then
    echo "Fatal error: Cannot start program because directory:"
    echo "$bindir"
    echo "Does not exist."
    exit 1
endif

set prog="$1"

if ( ! -e "$bindir/$prog" ) then
    if ( -e "$bindir/Release/$prog" ) then
	set bindir="$bindir/Release"
    else if ( -e "$bindir/Debug/$prog" ) then
	set bindir="$bindir/Debug"
    endif
endif

setenv XAPPLRESDIR	"$DTECT_APPL/data"
setenv PATH		"${bindir}:${PATH}"

unsetenv QTDIR

if ( $extrascript != "" ) then
    source $extrascript
endif

set exitcode = 0
if ( $issys == yes ) then

    if ( $?DTECT_SCRIPT_DEBUG ) then
	echo "$0 : $xtermcmd $nicecmd $*:q"
    endif

    if ( $background == yes ) then
	$xtermcmd $nicecmd $*:q &
	set exitcode = $status
	sleep 1 # Needed for Multi-machine processing with DTECT_DEBUG on.
    else
	$xtermcmd $nicecmd $*:q
	set exitcode = $status
    endif
else
    if ( $?DTECT_SCRIPT_DEBUG ) then
	echo "$0 : $xtermcmd $nicecmd $bindir/$*:q"
    endif

    if ( $?DTECT_PRELOAD ) then
    	setenv LD_PRELOAD $DTECT_PRELOAD
    endif

    if ( $background == yes ) then
	$xtermcmd $nicecmd $bindir/$*:q &
	set exitcode = $status
	sleep 1 # Needed for Multi-machine processing with DTECT_DEBUG on.
    else
	$xtermcmd $nicecmd $bindir/$*:q
	set exitcode = $status
    endif
endif

exit $exitcode

