#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id$
#
# module startup script
#_______________________________________________________________________________


if ( $?DTECT_SCRIPT_VERBOSE ) then
    echo "$0  ++++++"
    echo "args: $*"
    set verbose=yes
    set echo=on
endif

set SCRIPTDIR = `dirname $0`
set DTECT_APPL  = `dirname ${SCRIPTDIR}`

eval `"$DTECT_APPL/bin/mksethdir"`
if ( $binsubdir == luxia64 ) then
	echo "Warning: IA64 Linux not supported. Trying x32 emulation."
	set binsubdir=lux32
endif

set bindir="$DTECT_APPL/bin/$binsubdir"
set nicecmd=""
set debug=no
set extrascript=""
set xtermcmd=""
set indosbox=no
set askclosebox=no
set issys=no
set background=no

next_arg:

if ( "$1" == "--sys" ) then

    set issys=yes
    shift
    goto next_arg

else if ( "$1" == "--inbg" ) then

    set background=yes
    shift
    goto next_arg

else if ( "$1" == "--debug" ) then
    if ( -e $bindir/Debug/libBasic.so ) then
	set bindir=$bindir/Debug
	echo "Using debug bindir: $bindir"
    endif
    shift
    goto next_arg

else if ( "$1" == "--release" ) then
    set bindir=$bindir/Release
    echo $bindir
    shift
    goto next_arg

else if ( "$1" == "--nice" ) then

    set niceness=$2
    set nicecmd=/usr/bin/nice
    if ( -x /bin/nice ) set nicecmd=/bin/nice
    if ( ! -x $nicecmd || $?COMSPEC ) then
	set nicecmd=""
    else
	set nicecmd="$nicecmd -$2"
    endif

    shift ; shift
    goto next_arg

else if ( "$1" == "--prescript" ) then

    set extrascript="$2"
    if ( ! -e $extrascript ) then
	if ( -e "$DTECT_APPL/bin/$extrascript" ) then
	    set extrascript="$DTECT_APPL/bin/$extrascript"
	else if ( -e "$bindir/$extrascript" ) then
	    set extrascript="$bindir/$extrascript"
	endif
    endif

    shift ; shift
    goto next_arg

else if ( "$1" == "--inxterm" || "$1" == "--inxterm+askclose" ) then

    if ( $HDIR == win ) then

        set indosbox=yes
        if ( "$1" == "--inxterm+askclose" ) then
            set askclosebox=yes
        endif

    else

	set xtermcmdarg=""
	if ( "$1" == "--inxterm+askclose" ) then
	    set xtermcmdarg = "--askclose"
	endif

	set xtermpath=xterm
	which xterm >&! /dev/null
	set result=$status
	if ( $result == 0 ) set xtermpath=`which xterm` >&! /dev/null

	if ( ! -x $xtermpath ) set result=1
	
	if ( $result != 0 ) then
	    set xtermpath=/usr/bin/X11/xterm
	    if ( $HDIR == sun5 ) then
	        set xtermpath=/usr/openwin/bin/xterm
		if ( ! -x $xtermpath ) set xtermpath=/usr/bin/xterm
	    endif
	    if ( $HDIR == mac ) set xtermpath="$DTECT_APPL/bin/mac_term"
	endif

	if ( $HDIR == mac ) then
	    set xtermcmd="$DTECT_APPL/bin/mac_term $xtermcmdarg"
	else
	    set xtermcmd="$xtermpath -bg lightyellow -fg black -geometry 80x25 -e $DTECT_APPL/bin/od_xterm_exec $DTECT_APPL $xtermcmdarg"
	endif

    endif

    shift
    goto next_arg

endif


if ( ! -d "$bindir" ) then
    echo "Fatal error: Cannot start program because directory:"
    echo "$bindir"
    echo "Does not exist."
    exit 1
endif

set prog="$1"

if ( ! -e "$bindir/$prog" ) then
    if ( $?DEBUG && -e "$bindir/Debug/$prog" ) then
	set bindir="$bindir/Debug"
    else if ( -e "$bindir/Release/$prog" ) then
	set bindir="$bindir/Release"
    endif
    if ( ! -e "$bindir/$prog" ) then
    	if ( $binsubdir == lux64 && -e "$DTECT_APPL/bin/lux32/$prog" ) then
	    set binsubdir=lux32
	    set bindir="$DTECT_APPL/bin/$binsubdir"
	endif
    endif
endif

setenv XAPPLRESDIR	"$DTECT_APPL/data"
setenv PATH		"${bindir}:${PATH}"

if ( $HDIR == mac ) then
    if ( $?DYLD_LIBRARY_PATH ) then
	setenv DYLD_LIBRARY_PATH	"${bindir}/so:${DYLD_LIBRARY_PATH}"
    else
	setenv DYLD_LIBRARY_PATH	"${bindir}/so:/lib:/usr/lib"
    endif
else
    if ( $?LD_LIBRARY_PATH ) then
	setenv LD_LIBRARY_PATH	"${bindir}/so:${LD_LIBRARY_PATH}"
    else
	setenv LD_LIBRARY_PATH	"${bindir}/so:/lib:/usr/lib"
    endif
endif

if ( $HDIR == sgi ) then
    if ( $?LD_LIBRARYN32_PATH ) then
        setenv LD_LIBRARYN32_PATH "${bindir}/so:${LD_LIBRARYN32_PATH}"
    else
        setenv LD_LIBRARYN32_PATH "${bindir}/so:/lib:/usr/lib"
    endif
endif

unsetenv QTDIR

if ( $extrascript != "" ) then
    source $extrascript
endif


set argsnospaces=""
set argswithspaces=""

more_args:

if ( "$1" != "" ) then

    set arg="$1"
    set hasspacechar = `echo "$arg" | sed -n "s/[ ].*//p" | wc -c`
    if( ${hasspacechar} ) then
	if ( "$argswithspaces" == "" ) then
	    set argswithspaces="$arg"
	else
	    set argswithspaces="$argswithspaces $arg"
	endif
    else
	if ( "$argsnospaces" == "" ) then
	    set argsnospaces="$arg"
	else
	    set argsnospaces="$argsnospaces $arg"
	endif
    endif

    shift
    goto more_args

endif

set exitcode = 0
if ( $issys == yes ) then

    if ( $?DTECT_SCRIPT_DEBUG ) then
	echo "$0 : $xtermcmd $nicecmd $argsnospaces $argswithspaces"
    endif

    if ( $background == yes ) then
	$xtermcmd $nicecmd "$argsnospaces" $argswithspaces:q &
	set exitcode = $status
    else
	$xtermcmd $nicecmd "$argsnospaces" $argswithspaces:q
	set exitcode = $status
    endif
else
    if ( $?DTECT_SCRIPT_DEBUG ) then
	echo "$0 : $xtermcmd $nicecmd $bindir/$argsnospaces $argswithspaces"
    endif

    if ( $?DTECT_PRELOAD ) then
    	setenv LD_PRELOAD $DTECT_PRELOAD
    endif

    if ( $background == yes ) then
	$xtermcmd $nicecmd "$bindir"/$argsnospaces $argswithspaces:q &
	set exitcode = $status
    else
	$xtermcmd $nicecmd "$bindir"/$argsnospaces $argswithspaces:q
	set exitcode = $status
    endif
endif

exit $exitcode
