#!/bin/csh -f
#________________________________________________________________________
#
# Copyright:    (C) 1995-2022 dGB Beheer B.V.
# License:      https://dgbes.com/licensing
#________________________________________________________________________
#
# OpenTect environment initialization script 
#

if ( ${binsubdir} != lux64 ) then
    exit 0
endif

if ( -d /usr/lib64 ) then
   set sysglxlibdir=/usr/lib64
else if ( -d /usr/lib ) then
   set sysglxlibdir=/usr/lib
endif

if ( -d /usr/X11R6/lib64 ) then
   set nvglxlibdir=/usr/X11R6/lib64
else if ( -d /usr/X11R6/lib ) then
   set nvglxlibdir=/usr/X11R6/lib
else
    exit 0
endif

set glliblist=`/sbin/ldconfig -p | grep libGL.so | grep x86-64`

#creating an empty list
set gllist = ( )
foreach file ( ${glliblist} )
    if ( $file =~ *'/'* ) then
	if ( -e $file && -l $file ) then
	    #"Getting target file name if file is link"
	    set glfilenm = `readlink -f $file`
	else if ( -e $file ) then
	    set glfilenm=$file
	endif

	set duplicate=no
	foreach glfile ( ${gllist} )
	    if ( $glfile =~ $glfilenm )  then
		set duplicate=yes
	    endif
	end

	if ( $duplicate == no ) then
	    set gllist = ( $gllist "$glfilenm" )
	endif
    endif
end

@ libglcount = 0
foreach glfile ( ${gllist} )
    @ libglcount++
end

if ( ${libglcount} == "0" ) then
    echo "No OpenGL library found. Cannot start OpendTect."
    exec /bin/false
else if ( ${libglcount} == "1" ) then
    exit 0
else if ( ! $?OD_SYSTEM_LIBRARY_PATH ) then
    # Input LD_LIBRARY_PATH was empty: The system can handle duplicates
    exit 0
endif

set mesafound=false
set nvidiafound=false
foreach libpath ( $OD_SYSTEM_LIBRARY_PATH:as/:/ /  )
    if ( ${libpath} == ${sysglxlibdir} ) then
	if ( -e ${libpath}/libGL.so ) then
	    set mesastrs=`strings ${libpath}/libGL.so | grep GLX_MESA | wc -l`
	    if ( ${mesastrs} > 0 ) then
		set mesafound=true
	    endif
	endif
    else if ( ${libpath} == ${nvglxlibdir} ) then
	if ( -e ${libpath}/libGL.so ) then
	    set nvidiastrs=`strings ${libpath}/libGL.so | grep GLX_nvidia | wc -l`
	    if ( ${nvidiastrs} > 0 ) then
		set nvidiafound=true
	    endif
	endif
    else if ( ${libpath} == ${nvglxlibdir} ) then
    endif
end

if ( ${mesafound} == "true" && ${nvidiafound} == "true" ) then
    echo "Your LD_LIBRARY_PATH should not include ${sysglxlibdir} if"
    echo "${sysglxlibdir} contains the mesa libGL.so library and"
    echo "${nvglxlibdir} contains the nvidia libGL.so library."
    echo -n "Cannot start OpendTect before ${sysglxlibdir} is removed from LD_LIBRARY_PATH"
    echo " (it should not be there anyway)."
    exec /bin/false
endif

